import Mapping
clear_color 1, 1, 1, 1 

// Define texture maps
COLOR_MAP -> "colorMap.png" 
DISPLACEMENT_MAP -> "displacementMap.png" 

font "Arial" 
font_size 16 

displaced_plane -> 
    locally 
        rotatey -20 
        rotatex -65 
        rotatez 45 
        // Set texture maps
        texture_unit 0 
        texture COLOR_MAP 
        texture_unit 1 
        texture DISPLACEMENT_MAP 
        // Apply displacement mapping
        displacement_mapping 200 
        // Define subdivided plane
        plane -50, 0, 500, 500, 200, 200 


// Legend to the color map
legend_color -> 
    locally 
        texture COLOR_MAP 
        rectangle 460, 390, 200, 200 
        text_box 460, 250, 200, 50, 
            color "black" 
            font "Droid Sans", 24 
            align_center 
            vertical_align_center 
            text "Color map" 

// Legend to the displacement map
legend_height -> 
    locally 
        texture DISPLACEMENT_MAP 
        rectangle 460, 90, 200, 200 
        text_box 460, -60, 200, 50, 
            color "black" 
            font "Droid Sans", 24 
            align_center 
            vertical_align_center 
            text "Displacement map" 

draw_scene -> 
    color "white" 
    translate -100, -150, 0 
    displaced_plane 
    // Set legends
    legend_color 
    legend_height 

S -> min (window_width / 1000, window_height / 700) 
scale S, S, 1.0 
draw_scene
