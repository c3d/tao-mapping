// Mapping module definition
//
// This module defines mapping techniques.
//
// Copyright 20010-2011 Taodyne

module_description
    id "a2cdbc99-ce81-42b2-a218-f51938ef2414"
    name "Mapping"
    import_name "Mapping"
    description "Allow to add details in a scene thanks to a surface texture"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version "1.0"

module_description "fr",
    name "Mappage"
    description "Permet d'ajouter des détails visuels dans une scène à partir d'images."

// -------------------------------------------------------------------------------------------------------------------
//   ALPHA MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Alpha_mapping
// -------------------------------------------------------------------------------------------------------------------

//Define alpha thresold
ALPHA_THRESHOLD -> 0.01
ALPHA_THRESHOLD := 0.01

alpha_mapping n:real ->
    /**
    * Apply alpha mapping.
    **/
    ALPHA_THRESHOLD := n
    alpha_shader

alpha_shader ->
    /**
    * Define alpha shader.
    **/
    shader_program
        shader_log
        vertex_shader <<
            varying vec4 color;
            void main()
            {
                // Compute position
                gl_Position = ftransform();

                // Compute texture coordinates
                gl_TexCoord[0] = gl_TextureMatrix[0] * gl_MultiTexCoord0;
                gl_TexCoord[1] = gl_TextureMatrix[1] * gl_MultiTexCoord1;

                color = gl_Color;
            }
        >>
        fragment_shader <<
            // Mapping paramaters
            uniform float     threshold;
            uniform sampler2D alphaMap;
            uniform sampler2D colorMap;

            varying vec4 color;
            void main()
            {
                vec4 alpha_color = texture2D(alphaMap, gl_TexCoord[1].st);

                // Filter alpha map according to the threshold
                if(alpha_color.r < threshold && alpha_color.g < threshold && alpha_color.b < threshold)
                    discard;

                gl_FragColor  = texture2D(colorMap, gl_TexCoord[0].st) * color;
            }
        >>
    shader_set colorMap  := 0
    shader_set alphaMap  := 1
    shader_set threshold := ALPHA_THRESHOLD

// -------------------------------------------------------------------------------------------------------------------
//   DISPLACEMENT MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Displacement_mapping
//   or http://http.developer.nvidia.com/GPUGems2/gpugems2_chapter08.html
// -------------------------------------------------------------------------------------------------------------------

// Define displacement ratio
DISPLACEMENT_RATIO -> 0.0
DISPLACEMENT_RATIO := 0.0

displacement_mapping ratio:real ->
    /**
    * Apply displacement mapping.
    **/
    DISPLACEMENT_RATIO := ratio
    displacement_shader

displacement_shader ->
    /**
    * Define displacement shader.
    **/
    shader_program
        shader_log
        vertex_shader <<
            // Mapping paramaters
            uniform float     ratio;
            uniform sampler2D displacementMap;

            varying vec4 color;
            void main()
            {
               vec4 newPos, dv;
               float df;

               // Compute texture coordinates
               gl_TexCoord[0] = gl_TextureMatrix[0] *  gl_MultiTexCoord0;
               gl_TexCoord[1] = gl_TextureMatrix[1] *  gl_MultiTexCoord1;

               // Calculate new vertex position according to its normal and its texture coordinates
               dv     = texture2D( displacementMap, gl_TexCoord[1].st );
               df     = 0.30 * dv.x + 0.59 * dv.y + 0.11 * dv.z;
               newPos = vec4(df * ratio * gl_Normal, 0.0) + gl_Vertex;

               // Compute position
               gl_Position = gl_ModelViewProjectionMatrix * newPos;

               color = gl_Color;
            }
        >>
        fragment_shader <<
            uniform sampler2D colorMap;

            varying vec4 color;
            void main()
            {
               gl_FragColor = texture2D(colorMap, gl_TexCoord[0].xy) * color;
            }
        >>
    shader_set colorMap        := 0
    shader_set displacementMap := 1
    shader_set ratio           := DISPLACEMENT_RATIO

// -------------------------------------------------------------------------------------------------------------------
//   NORMAL MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Normal_mapping
// -------------------------------------------------------------------------------------------------------------------

normal_mapping ->
    /**
    * Apply normal mapping.
    **/
    normal_shader

normal_shader ->
    /**
    * Define normal shader.
    **/
    shader_program
        shader_log
        vertex_shader <<#extension GL_EXT_gpu_shader4 : require
            #extension GL_EXT_gpu_shader4 : enable
            uniform int lights;

            varying vec3 lightDir[8];
            varying vec3 viewDir;
            varying vec3 normal;
            varying vec4 color;
            void main()
            {
                // Determine object parameters
                gl_Position    = ftransform();

                // Compute texture coordinates
                gl_TexCoord[0] = gl_TextureMatrix[0] * gl_MultiTexCoord0;
                gl_TexCoord[1] = gl_TextureMatrix[1] * gl_MultiTexCoord1;

                // Compute world position and normal
                normal  = gl_NormalMatrix * gl_Normal;
                viewDir = -vec3(gl_ModelViewMatrix * gl_Vertex);

                // Compute light direction
                if(lights > 0)
                    for(int i = 0; i < lights; i++)
                        if(lights & (i + 1))
                            lightDir[i] = gl_LightSource[i].position.xyz + viewDir;

                color = gl_Color;
            }
        >>
        fragment_shader <<#extension GL_EXT_gpu_shader4 : require
            #extension GL_EXT_gpu_shader4 : enable
            // Mapping paramaters
            uniform sampler2D colorMap;
            uniform sampler2D normalMap;

            uniform int lights;

            varying vec3 lightDir[8];
            varying vec3 viewDir;
            varying vec3 normal;
            varying vec4 color;

            /**
            /* Calculate tangent space matrix from normal, vector in plane
            *  and texture coordinates
            */
            mat3 computeTangentFrame(vec3 normal, vec3 position, vec2 texCoord)
            {
                // Compute derivatives
                vec3 dpx = dFdx(position);
                vec3 dpy = dFdy(position);
                vec2 dtx = dFdx(texCoord);
                vec2 dty = dFdy(texCoord);

                // Compute tangent and binormal
                vec3 tangent  = normalize(dpy * dtx.t - dpx * dty.t);
                vec3 binormal = cross(tangent, normal);

                return mat3(tangent, binormal, normal);
            }

            /**
            /* Compute render color according to materials, lights and colors
            *  parameters in the current scene in the current scene
            */
            vec4 computeRenderColor(vec3 N, vec3 V, vec4 renderColor)
            {
                vec4 lighting_color;
                if(lights > 0)
                {
                    vec4 ambient, diffuse, specular;
                    ambient  = gl_FrontLightModelProduct.sceneColor * gl_FrontMaterial.ambient;

                    for(int i = 0; i < lights; i++)
                    {
                        if(lights & (i + 1))
                        {
                            vec3 L = normalize(lightDir[i]);

                            // Compute ambient part
                            ambient += gl_LightSource[i].ambient * gl_FrontMaterial.ambient;

                            // Diffuse coefficient
                            float nDotL = max(clamp(dot(L, N), 0.0, 1.0), 0.0);
                            if (nDotL > 0.0)
                            {
                                // Compute diffuse part
                                diffuse += gl_LightSource[i].diffuse * gl_FrontMaterial.diffuse * nDotL;

                                // Compute specular coefficient
                                float nDotV = clamp(dot(reflect(-L, N), V), 0.0, 1.0);
                                if (nDotV > 0.0)
                                {
                                    // Compute specular part
                                    specular += gl_LightSource[i].specular * gl_FrontMaterial.specular * pow(nDotV, gl_FrontMaterial.shininess);
                                }
                            }
                        }
                    }
                     // Define new render color
                    lighting_color  = (ambient + diffuse) * renderColor + specular;
                }
                else
                {
                    // Define new render color
                    lighting_color  = color * renderColor;
                }

                return lighting_color;
            }

            void main()
            {
                // Normalize vectors
                vec3 N = normalize(normal);
                vec3 V = normalize(viewDir);

                // Compute bump coefficient according to normal map
                vec3 bump = normalize(texture2D(normalMap, gl_TexCoord[1].st).xyz * 2.0 - 1.0);

                // Calculate tangent space matrix
                mat3 TBN = computeTangentFrame(N, V, gl_TexCoord[1].st);

                // Define new normal
                N = TBN * bump;

                // Compute render color
                vec4 renderColor = texture2D(colorMap, gl_TexCoord[0].st);

                gl_FragColor = computeRenderColor(N, V, renderColor);
            }
        >>
    shader_set lights   := light_id
    shader_set colorMap  := 0
    shader_set normalMap := 1

// -------------------------------------------------------------------------------------------------------------------
//   NOISE MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Image_noise
// -------------------------------------------------------------------------------------------------------------------

// Define noise ratio
NOISE_RATIO -> 0.0
NOISE_RATIO := 0.0

noise_mapping n->
    /**
    * Apply noise mapping.
    **/
    NOISE_RATIO := n
    noise_shader

noise_map w:integer, h:integer->
    /**
    * Generate a random noise image.
    **/
    frame_texture w, h,
        color "white"
        generate_noise
        rectangle 0, 0, w, h

noise_shader ->
    /**
    * Define noise shader.
    **/
    shader_program
        shader_log
        vertex_shader <<
            uniform int lights;

            varying vec3 lightDir[8];
            varying vec3 viewDir;
            varying vec3 normal;
            varying vec4 color;
            void main()
            {
                // Compute position
                gl_Position    = ftransform();

                // Compute texture coordinates
                gl_TexCoord[0] = gl_TextureMatrix[0] * gl_MultiTexCoord0;
                gl_TexCoord[1] = gl_TextureMatrix[1] * gl_MultiTexCoord1;

                // Compute world position and normal
                normal  = gl_NormalMatrix * gl_Normal;
                viewDir = -vec3(gl_ModelViewMatrix * gl_Vertex);

                // Compute light direction
                if(lights > 0)
                    for(int i = 0; i < lights; i++)
                        if(lights & (i + 1))
                            lightDir[i] = gl_LightSource[i].position.xyz + viewDir;

                color = gl_Color;
            }
        >>
        fragment_shader <<#extension GL_EXT_gpu_shader4 : require
            #extension GL_EXT_gpu_shader4 : enable
            // Mapping parameters
            uniform float     ratio;
            uniform sampler2D colorMap;
            uniform sampler2D noiseMap;

            uniform int lights;

            varying vec3 lightDir[8];
            varying vec3 viewDir;
            varying vec3 normal;
            varying vec4 color;
            /**
            /* Compute render color according to materials, lights and colors parameters in the current scene
            *  in the current scene
            */
            vec4 computeRenderColor(vec4 renderColor)
            {
                vec4 lighting_color;
                if(lights > 0)
                {
                    vec3 N = normalize(normal);
                    vec3 V = normalize(viewDir);

                    vec4 ambient, diffuse, specular;
                    ambient  = gl_FrontLightModelProduct.sceneColor * gl_FrontMaterial.ambient;

                    for(int i = 0; i < lights; i++)
                    {
                        if(lights & (i + 1))
                        {
                            vec3 L = normalize(lightDir[i]);

                            // Compute ambient part
                            ambient += gl_LightSource[i].ambient * gl_FrontMaterial.ambient;

                            // Diffuse coefficient
                            float nDotL = max(clamp(dot(L, N), 0.0, 1.0), 0.0);
                            if (nDotL > 0.0)
                            {
                                // Compute diffuse part
                                diffuse += gl_LightSource[i].diffuse * gl_FrontMaterial.diffuse * nDotL;

                                // Compute specular coefficient
                                float nDotV = clamp(dot(reflect(-L, N), V), 0.0, 1.0);
                                if (nDotV > 0.0)
                                {
                                    // Compute specular part
                                    specular += gl_LightSource[i].specular * gl_FrontMaterial.specular * pow(nDotV, gl_FrontMaterial.shininess);
                                }
                            }
                        }
                    }

                     // Define new render color
                    lighting_color  = (ambient + diffuse) * renderColor + specular;
                }
                else
                {
                    // Define new render color
                    lighting_color  = color * renderColor;
                }

                return lighting_color;
            }

            void main()
            {
                // Get noise value from noise map.
                vec4 noise = texture2D(noiseMap, gl_TexCoord[1].st );

                // Compute noise coordinates
                vec2 noiseCoord = gl_TexCoord[0].st + vec2( noise.r * 0.065, -noise.g * 0.025 ) *  ratio / 10.0;

                vec4 renderColor = texture2D(colorMap, noiseCoord);

                gl_FragColor = computeRenderColor(renderColor);
            }
        >>
    shader_set colorMap := 0
    shader_set noiseMap := 1
    shader_set ratio    := NOISE_RATIO
    shader_set lights   := light_id

generate_noise ->
    /**
    * Generate noise effect.
    **/
    shader_program
        shader_log
        vertex_shader <<
            varying vec4 color;
            void main()
            {
               gl_TexCoord[0] = gl_Vertex;
               gl_Position    = ftransform();

               color = gl_Color;
            }
        >>
        fragment_shader <<#extension GL_EXT_gpu_shader4 : require
            #extension GL_EXT_gpu_shader4 : enable
            // Pseudo random number base generator use to make noise

            varying vec4 color;
            float noise(vec2 v)
            {
                int n;
                n = int(v.x * 40.0 + v.y * 6400.0);
                n = (n << 13) ^ n;

                return 1.0 - float( (n * (n * n * 15731 + 789221) + 1376312589) & 0x7fffffff) / 1073741824.0;
            }

            void main()
            {
                float noiseColor = (noise(gl_TexCoord[0].st) + 1.0) * 0.5;
                gl_FragColor     = vec4(noiseColor, noiseColor, noiseColor, 1.0) * color;
            }
        >>

// -------------------------------------------------------------------------------------------------------------------
//   CUBE MAP
//   For further informations, see http://wiki.polycount.com/CubeMap
// -------------------------------------------------------------------------------------------------------------------

cube_map t:tree->
    /**
    * Create a cube map.
    **/
    cube_map_shader
    texture_cube t
    texture 0 //Force desactivation of cube map

cube_map_shader ->
    /**
    * Define shader to apply correctly cube map.
    **/
    shader_program
        shader_log
        vertex_shader <<
            void main()
            {
                // Generate texture coordinates (equivalent to glTexGen)
                vec4 xPlane = vec4( 1.0, 0.0, 0.0, 0.0 );
                vec4 yPlane = vec4( 0.0, 1.0, 0.0, 0.0 );
                vec4 zPlane = vec4( 0.0, 0.0, 1.0, 0.0 );

                gl_TexCoord[0].x = dot(gl_TextureMatrix[0] * vec4(gl_Vertex.xyz, 1.0), xPlane);
                gl_TexCoord[0].y = dot(gl_TextureMatrix[0] * vec4(gl_Vertex.xyz, 1.0), yPlane);
                gl_TexCoord[0].z = dot(gl_TextureMatrix[0] * vec4(gl_Vertex.xyz, 1.0), zPlane);

                // Compute position
                gl_Position = ftransform();
            }
        >>
        fragment_shader <<
            // Mapping parameters
            uniform samplerCube cubeMap;
            void main()
            {
                vec3 cube     = textureCube(cubeMap, gl_TexCoord[0].xyz).rgb;
                gl_FragColor  = vec4(cube, 1.0);
            }
        >>
    shader_set cubeMap := texture_unit

// -------------------------------------------------------------------------------------------------------------------
//   CUBE MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Cube_mapping
// -------------------------------------------------------------------------------------------------------------------

// Define cube mapping ratio
CUBE_RATIO -> 0.0
CUBE_RATIO := 0.0

cube_mapping r:real->
    /**
    * Apply cube mapping.
    **/
    CUBE_RATIO := r
    cube_shader

cube_shader ->
    /**
    * Define cube mapping shader.
    **/
    shader_program
        shader_log
        vertex_shader <<#version 120

            uniform mat4 model;
            uniform vec3 camera;
            void main()
            {
                mat3 normalMatrix;

                // Get linear part of model matrix
                normalMatrix[0][0] = model[0][0];
                normalMatrix[0][1] = model[0][1];
                normalMatrix[0][2] = model[0][2];

                normalMatrix[1][0] = model[1][0];
                normalMatrix[1][1] = model[1][1];
                normalMatrix[1][2] = model[1][2];

                normalMatrix[2][0] = model[2][0];
                normalMatrix[2][1] = model[2][1];
                normalMatrix[2][2] = model[2][2];

                // Compute world position and normal
                vec4 worldPos = model * gl_Vertex;
                vec3 normal   = normalize(normalMatrix * gl_Normal);
                vec3 eyePos   = normalize(worldPos.xyz - camera);

                // Compute reflection vector
                vec3 r = reflect (eyePos, normal);

                // Compute texture coordinates
                gl_TexCoord[1] = gl_TextureMatrix[1] * vec4 (r, 1.0);
                gl_TexCoord[0] = gl_TextureMatrix[0] * gl_MultiTexCoord0;

                // Compute position
                gl_Position = ftransform();
            }
        >>
        fragment_shader <<
            // Mapping parameters
            uniform float       ratio;
            uniform sampler2D   colorMap;
            uniform samplerCube cubeMap;

            void main()
            {
                vec3 cube     = textureCube(cubeMap, gl_TexCoord[1].xyz).rgb;
                vec3 color    = texture2D(colorMap, gl_TexCoord[0].st).rgb;
                gl_FragColor  = vec4(mix(color, cube, ratio), 1.0);
            }
        >>
    shader_set colorMap := 0
    shader_set cubeMap  := 1
    shader_set ratio    := CUBE_RATIO

    //FOLLOWING PARAMETERS ARE NEEDED BUT NOT OPTIMIZED BECAUSE LISTS ARE RELOAD FULLY TO EACH XL FUNCTIONS CALL
    shader_set camera   := (camera_position at 1; camera_position at 2; camera_position at 3)
    shader_set model    := (model_matrix at 1;  model_matrix at 2;  model_matrix at 3;  model_matrix at 4;
                            model_matrix at 5;  model_matrix at 6;  model_matrix at 7;  model_matrix at 8;
                            model_matrix at 9;  model_matrix at 10; model_matrix at 11; model_matrix at 12;
                            model_matrix at 13; model_matrix at 14; model_matrix at 15; model_matrix at 16)

// -------------------------------------------------------------------------------------------------------------------
//   SPHERE MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Sphere_mapping
// -------------------------------------------------------------------------------------------------------------------

// Define sphere mapping ratio
SPHERE_RATIO -> 0.0
SPHERE_RATIO := 0.0

sphere_mapping r:real->
    /**
    * Apply sphere mapping.
    **/
    SPHERE_RATIO := r
    sphere_shader

sphere_shader ->
    /**
    * Define sphere mapping shader.
    **/
    shader_program
        vertex_shader <<#version 120
        uniform mat4 model;
        uniform vec3 camera;
        void main()
        {
            mat3 normalMatrix;

            // Get linear part of model matrix
            normalMatrix[0][0] = model[0][0];
            normalMatrix[0][1] = model[0][1];
            normalMatrix[0][2] = model[0][2];

            normalMatrix[1][0] = model[1][0];
            normalMatrix[1][1] = model[1][1];
            normalMatrix[1][2] = model[1][2];

            normalMatrix[2][0] = model[2][0];
            normalMatrix[2][1] = model[2][1];
            normalMatrix[2][2] = model[2][2];

            // Determine object parameters
            gl_Position    = ftransform();
            gl_TexCoord[0] = gl_TextureMatrix[0] * gl_MultiTexCoord0;

            // Compute world position and normal
            vec4 worldPos = model * gl_Vertex;
            vec3 normal   = normalize(normalMatrix * gl_Normal);
            vec3 eyePos   = normalize(worldPos.xyz - camera);

            // Compute camera parameters
            vec3  r  = reflect( eyePos, normal );
            float m  = 2.0 * sqrt( r.x * r.x + r.y * r.y + (r.z + 1.0) * (r.z + 1.0) );

            // Compute sphere coordinates
            gl_TexCoord[1].s = r.x/m + 0.5;
            gl_TexCoord[1].t = r.y/m + 0.5;
        }
        >>
        fragment_shader <<
            // Mapping parameters
            uniform float     ratio;
            uniform sampler2D colorMap;
            uniform sampler2D sphereMap;

            void main()
            {
                vec3 color   = texture2D( colorMap, gl_TexCoord[0].st).rgb;
                vec3 sphere  = texture2D( sphereMap, gl_TexCoord[1].st).rgb;
                gl_FragColor = vec4(mix(color, sphere, ratio), 1.0);
            }
        >>
    shader_set colorMap  := 0
    shader_set sphereMap := 1
    shader_set ratio     := SPHERE_RATIO

    //FOLLOWING PARAMETERS ARE NEEDED BUT NOT OPTIMIZED BECAUSE LISTS ARE RELOAD FULLY TO EACH XL FUNCTIONS CALL
    shader_set camera   := (camera_position at 1; camera_position at 2; camera_position at 3)
    shader_set model    := (model_matrix at 1;  model_matrix at 2;  model_matrix at 3;  model_matrix at 4;
                            model_matrix at 5;  model_matrix at 6;  model_matrix at 7;  model_matrix at 8;
                            model_matrix at 9;  model_matrix at 10; model_matrix at 11; model_matrix at 12;
                            model_matrix at 13; model_matrix at 14; model_matrix at 15; model_matrix at 16)


// -------------------------------------------------------------------------------------------------------------------
//   REFLECTION MAPPING
//   For further informations, see http://en.wikipedia.org/wiki/Reflection_mapping
// -------------------------------------------------------------------------------------------------------------------

// Define reflection mapping ratio
REFLECTION_RATIO -> 0.0
REFLECTION_RATIO := 0.0

reflection_mapping r:real->
    /**
    * Apply reflection mapping.
    **/
    REFLECTION_RATIO := r
    reflection_shader

reflection_shader ->
    /**
    * Define reflection mapping shader.
    **/
    shader_program
        vertex_shader <<#version 120
            uniform mat4 model;
            uniform vec3 camera;
            void main()
            {
                mat3 normalMatrix;

                // Get linear part of model matrix
                normalMatrix[0][0] = model[0][0];
                normalMatrix[0][1] = model[0][1];
                normalMatrix[0][2] = model[0][2];

                normalMatrix[1][0] = model[1][0];
                normalMatrix[1][1] = model[1][1];
                normalMatrix[1][2] = model[1][2];

                normalMatrix[2][0] = model[2][0];
                normalMatrix[2][1] = model[2][1];
                normalMatrix[2][2] = model[2][2];

                // Compute world position and normal
                vec4 worldPos = model * gl_Vertex;
                vec3 normal   = normalize(normalMatrix * gl_Normal);
                vec3 eyePos   = normalize(worldPos.xyz - camera);

                // Compute reflection vector
                vec3 r = normalize(reflect (eyePos, normal));

                // Compute texture coordinates
                gl_TexCoord[0] = gl_TextureMatrix[0] * gl_MultiTexCoord0;
                gl_TexCoord[1].x = (r.x / 2.0) + 0.5 ;
                gl_TexCoord[1].y = (r.y / 2.0) + 0.5 ;

                // Compute position
                gl_Position = ftransform();
            }
        >>
        fragment_shader <<
            // Mapping parameters
            uniform float       ratio;
            uniform sampler2D   colorMap;
            uniform sampler2D   reflectionMap;

            void main()
            {
                vec3 reflection = texture2D(reflectionMap, gl_TexCoord[1].xy).rgb;
                vec3 color      = texture2D(colorMap, gl_TexCoord[0].st).rgb;
                gl_FragColor    = vec4(mix(color, reflection, ratio), 1.0);
            }
        >>
    shader_set ratio          := REFLECTION_RATIO
    shader_set colorMap       := 0
    shader_set reflectionMap  := 1

    //FOLLOWING PARAMETERS ARE NEEDED BUT NOT OPTIMIZED BECAUSE LISTS ARE RELOAD FULLY TO EACH XL FUNCTIONS CALL
    shader_set camera   := (camera_position at 1; camera_position at 2; camera_position at 3)
    shader_set model    := (model_matrix at 1;  model_matrix at 2;  model_matrix at 3;  model_matrix at 4;
                            model_matrix at 5;  model_matrix at 6;  model_matrix at 7;  model_matrix at 8;
                            model_matrix at 9;  model_matrix at 10; model_matrix at 11; model_matrix at 12;
                            model_matrix at 13; model_matrix at 14; model_matrix at 15; model_matrix at 16)



